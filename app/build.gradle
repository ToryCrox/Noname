apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

apply from: "$project.rootDir/tools/basic.gradle"
apply from: "$project.rootDir/tools/kotlin_support.gradle"



android {

    defaultConfig {
        versionCode getTotalGitCommitCount() + 10000
        applicationId "com.tory.noname"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // 定义proto文件目录
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //支持多个dex文件构建应用程序
    implementation project(':base:common')

    //support相关包
    implementation libraries.appcompat
    implementation libraries.design
    implementation libraries.cardview
    implementation libraries.recyclerview
    implementation libraries.preference
    implementation libraries.constraintLayout

    //rx
    implementation libraries.rxjava
    implementation libraries.rxandroid

    //okhttp
    implementation libraries.okhttp3
    implementation libraries.okhttp3Logging

    //retrofit
    implementation libraries.retrofit
    implementation libraries.adapterRxjava

    //gson
    implementation libraries.gson
    implementation libraries.converterGson
    //glide
    implementation libraries.glide
    implementation libraries.glideOkhttp3
    annotationProcessor libraries.glideCompiler

    implementation libraries.fastjson

    //stetho调试时使用
    implementation libraries.stetho
    implementation libraries.stethoOkhttp3

    implementation libraries.eleastictyview
    //butterknife引用
    implementation libraries.butterknife
    annotationProcessor libraries.butterknifeCompiler

    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.3.6'

    // koin 依赖注入
    implementation "org.koin:koin-android:${libVersions.koin}"
    implementation "org.koin:koin-android-viewmodel:${libVersions.koin}"
    // 可使用协程并可以观察生命周期的ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${libVersions.lifecycleKtxVersion}"
    implementation libraries.lifecycleKtx

    // 协程
    implementation libraries.kotlinCoroutineCore
    implementation libraries.kotlinCoroutineAndroid

    testImplementation libraries.junit

    debugImplementation libraries.leakcanary
    releaseImplementation libraries.leakcanaryNoOp
    testImplementation libraries.leakcanaryNoOp

    // 定义protobuf依赖，使用精简版
    implementation "com.google.protobuf:protobuf-lite:3.0.0"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
            }
        }
    }
}
