apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

apply from: "$project.rootDir/tools/basic.gradle"
apply from: "$project.rootDir/tools/kotlin_support.gradle"

apply from: "$project.rootDir/tools/show_dependencies.gradle"


android {

    defaultConfig {
        versionCode getTotalGitCommitCount() + 10000
        applicationId "com.tory.noname"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // 定义proto文件目录
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //支持多个dex文件构建应用程序
    implementation project(':base:common')
    implementation project(':module:koin')

    //support相关包
    implementation libraries.appcompatX
    implementation libraries.design
    implementation libraries.cardview
    implementation libraries.recyclerview
    implementation libraries.preference
    implementation libraries.constraintLayout

    //rx
    implementation libraries.rxjava
    implementation libraries.rxandroid

    //glide
    implementation libraries.glide
    implementation libraries.glideOkhttp3
    annotationProcessor libraries.glideCompiler

    implementation libraries.fastjson

    //stetho调试时使用
    implementation libraries.stetho
    implementation libraries.stethoOkhttp3

    implementation libraries.eleastictyview
    //butterknife引用
    implementation libraries.butterknife
    annotationProcessor libraries.butterknifeCompiler

    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.3.6'

    // koin 依赖注入
    implementation "org.koin:koin-android:${libVersions.koin}"
    implementation "org.koin:koin-android-viewmodel:${libVersions.koin}"
    // 可使用协程并可以观察生命周期的ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${libVersions.lifecycleKtxVersion}"
    implementation libraries.lifecycleKtx

    // 协程
    implementation libraries.kotlinCoroutineCore
    implementation libraries.kotlinCoroutineAndroid

    // paging库
    implementation "androidx.paging:paging-runtime:3.1.1"

    testImplementation libraries.junit

    debugImplementation libraries.leakcanary
    releaseImplementation libraries.leakcanaryNoOp
    testImplementation libraries.leakcanaryNoOp

    // 定义protobuf依赖，使用精简版
    implementation "com.google.protobuf:protobuf-lite:3.0.0"
}

protobuf {
    protoc {
        // for apple m1, please add protoc_platform=osx-x86_64 in $HOME/.gradle/gradle.properties
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.13.0:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.13.0"
        }
    }
    plugins {
        javalite {
            if (project.hasProperty('protoc_platform')) {
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0:${protoc_platform}"
            } else {
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
            }
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }

}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.tory.plugin:aleaf:1.0.0.8'
    }
}
apply plugin: "tory-aleaf"
aleaf {
    versionCode 1001

    dependencies {
        "implementation"
    }
}